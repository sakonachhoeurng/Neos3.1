#
# Neos.Neos:Page is the default object used for rendering in most sites,
# so Neos.Seo extends this default object
#
prototype(Neos.Neos:Page) {
	htmlTag {
		attributes {
			lang = ${String.replace(documentNode.context.dimensions.language[0], '_', '-')}
			lang.@if.languageDimensionExists = ${Configuration.setting('Neos.ContentRepository.contentDimensions.language') != null}
			lang.@if.onlyRenderWhenInLiveWorkspace = ${node.context.workspace.name == 'live'}
		}
	}
	head {
		titleTag {
			default = Neos.Fusion:Collection {
				// Retrieve all parent document nodes excluding the homepage
				collection = ${q(documentNode).add(q(documentNode).parents()).slice(0, -1).get()}
				itemName = 'node'
				iterationName = 'nodeIterator'
				// Implode node titles with a dash
				itemRenderer = ${q(node).property('title') + (nodeIterator.isLast ? '' : ' - ')}
				// Always add general site name as suffix
				@process.siteName = ${(value ? String.stripTags(value) + ' - ' : '') + site.context.currentSite.name}
			}

			content = ${q(node).property('titleOverride') ? q(node).property('titleOverride') : this.default}
		}

		metaDescriptionTag = Neos.Fusion:Tag {
			tagName = 'meta'
			attributes {
				name = 'description'
				content = ${q(node).property('metaDescription')}
			}
			@if.isNotBlank = ${!String.isBlank(q(node).property('metaDescription'))}
		}

		metaKeywordsTag = Neos.Fusion:Tag {
			tagName = 'meta'
			attributes {
				name = 'keywords'
				content = ${q(node).property('metaKeywords')}
			}
			@if.isNotBlank = ${!String.isBlank(q(node).property('metaKeywords'))}
		}

		metaRobotsTag = Neos.Fusion:Tag {
			tagName = 'meta'
			attributes {
				name = 'robots'
				content = ${(q(node).property('metaRobotsNoindex') ? 'noindex' : 'index') + ',' + (q(node).property('metaRobotsNofollow') ? 'nofollow' : 'follow')}
			}
		}

		canonicalLink = Neos.Fusion:Tag {
			tagName = 'link'
			attributes {
				rel = 'canonical'
				href = Neos.Neos:NodeUri {
					node = ${documentNode}
					absolute = TRUE
				}
				href.@process.canonical = ${q(node).property('canonicalLink') ? q(node).property('canonicalLink') : value}
			}
		}

		alternateLanguageLinks = Neos.Neos:DimensionMenu {
			@if.languageDimensionExists = ${Configuration.setting('Neos.ContentRepository.contentDimensions.language') != null}
			@if.onlyRenderWhenInLiveWorkspace = ${node.context.workspace.name == 'live'}

			localeToLanguage = ${String.replace((item.preset ? item.preset.values[0] : item.dimensions[dimension][0]), '_', '-')}

			dimension = 'language'
			templatePath = 'resource://Neos.Seo/Private/Templates/FusionObjects/AlternateLanguageLinks.html'
		}

		twitterCard = Neos.Fusion:Array {
			@if.isEnabledBlank = ${!String.isBlank(q(node).property('twitterCardType'))}
			@if.isEnabled = ${q(node).property('twitterCardType') != null}

			cardTypeTag = Neos.Fusion:Tag {
				tagName = 'meta'
				attributes {
					name = 'twitter:card'
					content = ${q(node).property('twitterCardType')}
				}
			}

			cardSiteTag = Neos.Fusion:Tag {
				tagName = 'meta'
				attributes {
					name = 'twitter:site'
					content = ${Configuration.setting('Neos.Seo.twitterCard.siteHandle')}
				}
				@if.isSet = ${Configuration.setting('Neos.Seo.twitterCard.siteHandle') != null}
			}

			cardCreatorTag = Neos.Fusion:Tag {
				tagName = 'meta'
				attributes {
					name = 'twitter:creator'
					content = ${q(node).property('twitterCardCreator')}
				}
				@if.isNotBlank = ${!String.isBlank(q(node).property('twitterCardCreator'))}
			}

			cardTitleTag = Neos.Fusion:Tag {
				tagName = 'meta'
				attributes {
					name = 'twitter:title'
					content = ${q(node).property('twitterCardTitle')}
				}
				@if.isNotBlank = ${!String.isBlank(q(node).property('twitterCardTitle'))}
			}

			cardDescriptionTag = Neos.Fusion:Tag {
				tagName = 'meta'
				attributes {
					name = 'twitter:description'
					content = ${q(node).property('twitterCardDescription') ? q(node).property('twitterCardDescription') : q(node).property('metaDescription')}
				}
				@if.isNotBlank = ${!String.isBlank((q(node).property('twitterCardDescription') ? q(node).property('twitterCardDescription') : q(node).property('metaDescription')))}
			}

			cardImageTag = Neos.Fusion:Tag {
				tagName = 'meta'
				attributes {
					name = 'twitter:image:src'
					content = Neos.Neos:ImageUri {
						asset = ${q(node).property('twitterCardImage')}
						maximumWidth = 2000
						maximumHeight = 2000
					}
				}
				@if.isImageSet = ${q(node).property('twitterCardImage') != null}
			}

			cardUrlTag = Neos.Fusion:Tag {
				tagName = 'meta'
				attributes {
					name = 'twitter:url'
					content = Neos.Neos:NodeUri {
						node = ${node}
						absolute = TRUE
					}
				}
			}
		}

		openGraphMetaTags = Neos.Fusion:Array {
			@context.openGraphType = ${q(node).property('openGraphType')}
			@if.isEnabled = ${!String.isBlank(openGraphType)}

			openGraphTypeTag = Neos.Fusion:Tag {
				tagName = 'meta'
				attributes {
					property = 'og:type'
					content = ${openGraphType}
				}
			}

			openGraphTitleTag = Neos.Fusion:Tag {
				tagName = 'meta'
				@context.openGraphTitle = ${q(node).property('openGraphTitle')}
				attributes {
					property = 'og:title'
					content = ${openGraphTitle}
				}
				@if.isNotBlank = ${!String.isBlank(openGraphTitle)}
			}

			openGraphDescriptionTag = Neos.Fusion:Tag {
				tagName = 'meta'
				@context.openGraphDescription = ${q(node).property('openGraphDescription') ? q(node).property('openGraphDescription') : q(node).property('metaDescription')}
				attributes {
					property = 'og:description'
					content = ${openGraphDescription}
				}
				@if.isNotBlank = ${!String.isBlank(openGraphDescription)}
			}

			openGraphImageTag = Neos.Fusion:Tag {
				tagName = 'meta'
				@context.openGraphImage = ${q(node).property('openGraphImage')}
				attributes {
					property = 'og:image'
					content = Neos.Neos:ImageUri {
						asset = ${openGraphImage}
						maximumWidth = 2000
						maximumHeight = 2000
					}
				}
				@if.isImageSet = ${openGraphImage != null && openGraphImage != ''}
			}

			openGraphUrlTag = Neos.Fusion:Tag {
				tagName = 'meta'
				attributes {
					property = 'og:url'
					content = Neos.Neos:NodeUri {
						node = ${node}
						absolute = TRUE
					}
				}
			}
		}
	}
}

prototype(Neos.Seo:XmlSitemap) < prototype(Neos.Fusion:Http.Message) {
	doctype = '<?xml version="1.0" encoding="UTF-8"?>'
	httpResponseHead.headers.Content-Type = 'text/xml'

	body = Menu {
		root = ${site}
		entryLevel = 0
		maximumLevels = 999
		renderHiddenInIndex = TRUE
		templatePath = 'resource://Neos.Seo/Private/Templates/Page/XmlSiteMap.xml'
		startingPoint = ${site}

		@cache.entryTags.1 = ${'DescendantOf_' + this.startingPoint.identifier}
	}
}

root.xmlSitemap {
	condition = ${request.format == 'xml.sitemap'}
	type = 'Neos.Seo:XmlSitemap'
}

